<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="300" height="100%"
		 initialize="initializeHandler(event)">
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			
			import potato.designer.framework.DesignerEvent;
			import potato.designer.framework.EventCenter;
			import potato.designer.plugin.uidesigner.DesignerConst;
			import potato.designer.plugin.uidesigner.UIDesignerHost;
			import potato.designer.plugin.uidesigner.ViewController;
			import potato.designer.plugin.uidesigner.basic.compiler.classdescribe.ClassProfile;
			import potato.designer.plugin.uidesigner.basic.compiler.classdescribe.IMemberProfile;
			import potato.designer.plugin.uidesigner.basic.interpreter.BasicTargetProfile;
			
			internal static var classProfile:ClassProfile;
			
			protected function initializeHandler(event:FlexEvent):void
			{
				EventCenter.addEventListener(DesignerConst.FOLD_FOCUS_CHANGED, updateHandler);
				EventCenter.addEventListener(DesignerConst.UPDATE, updateHandler);
			}
			
			protected function updateHandler(event:DesignerEvent):void
			{
				refresh();
			}
			
			protected function refresh():void
			{
				if(-1 == ViewController.focusIndex)
				{
					list.dataProvider = null;
					return;
				}
				var path:Vector.<uint> = ViewController.foldPath;
				path.push(ViewController.focusIndex);
				var tp:BasicTargetProfile = UIDesignerHost.getCompilerProfileByPath(path).targetProfile as BasicTargetProfile;
				if(!tp)
				{
					list.dataProvider = null;
					return;
				}
				
				var dp:ArrayList = new ArrayList;
				var cp:ClassProfile = BasicCompiler.getClassProfileByClassName(tp.className);
				var addedMap:Object = {};
				//添加构造方法
				if(cp.constructor)
				{
					dp.addItem([tp, cp.constructor, tp.constructorParam]);
				}
				var mp:IMemberProfile;
				//添加具有值的成员
				for(var i:int = 0; i < tp.membersName.length; i++)
				{
					mp = cp.getMember(tp.membersName[i]);
					addedMap[mp.name] = true;
					var param:Vector.<Object> = tp.membersParam[i];
					dp.addItem([tp, mp, param]);
				}
				//添加没有值的属性
				for each(mp in cp.accessors)
				{
					if(mp.enable && !addedMap[mp.name])
					{
						dp.addItem([tp, mp, null]);
					}
				}
				//添加方法
				for each(mp in cp.methods)
				{
					if(mp.enable)
					{
						dp.addItem([tp, mp, null]);
					}
				}
				
				
				
				list.dataProvider = dp;
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<s:Label text="成员视图"/>
	<s:List id="list" width="100%" height="100%" itemRenderer="potato.designer.plugin.uidesigner.basic.compiler.MemberViewItem"></s:List>
</s:Group>
