<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
				xmlns:s="library://ns.adobe.com/flex/spark"
				xmlns:mx="library://ns.adobe.com/flex/mx"
				autoDrawBackground="true" click="editHandler(event)"
				creationComplete="creationCompleteHandler(event)"
				dataChange="dataChangeHandler(event)" fontSize="16">
	<s:layout>
		<s:HorizontalLayout verticalAlign="middle"/>
	</s:layout>
	<fx:Script>
		<![CDATA[
			import flash.filesystem.File;
			
			import flashx.textLayout.conversion.ITextImporter;
			import flashx.textLayout.conversion.TextConverter;
			
			import mx.collections.ArrayList;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			import mx.utils.StringUtil;
			
			import potato.designer.plugin.uidesigner.UIDesignerHost;
			import potato.designer.plugin.uidesigner.basic.compiler.classdescribe.AccessorProfile;
			import potato.designer.plugin.uidesigner.basic.compiler.classdescribe.IMemberProfile;
			import potato.designer.plugin.uidesigner.basic.compiler.classdescribe.ITypeValue;
			import potato.designer.plugin.uidesigner.basic.compiler.classdescribe.MethodProfile;
			import potato.designer.plugin.uidesigner.basic.compiler.classdescribe.ParameterProfile;
			import potato.designer.plugin.uidesigner.basic.interpreter.BasicTargetProfile;
			
			import spark.events.TextOperationEvent;
			import spark.utils.TextFlowUtil;
			
			/**类型描述：变量和存取器*/
			protected var access:AccessorProfile;
			/**类型描述：方法*/
			protected var method:MethodProfile;
			/**已有参数。如果是未启用的成员，则此值为空*/
			protected var params:Vector.<*>;
			/**编辑参数。由于可以指定"undefined"（表示尚未配置该值），所以使用*类型。*/
			protected var editParams:Vector.<*>;
			/**编辑点。指示当前正在编辑哪个参数。为-1说明未启用编辑器。*/
			protected var editPoint:int = -1;
			protected var inputIsNull:Boolean;
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				input.addEventListener(TextOperationEvent.CHANGE, input_changeHandler);
				input.addEventListener(KeyboardEvent.KEY_DOWN, input_keyDownHandler);
				input.addEventListener(FocusEvent.FOCUS_OUT, inputOverHandler);
			}
			
			protected function input_changeHandler(event:TextOperationEvent):void
			{
			}
			
			protected function input_keyDownHandler(event:KeyboardEvent):void
			{
				if(Keyboard.LEFT == event.keyCode)
				{
					if(0 == input.selectionAnchorPosition && 0 == input.selectionActivePosition)
					{
						if(-1 != editPoint)
							editParams[editPoint] = inputIsNull ? null : input.text;
						
						previous();
					}
				}
				else if(Keyboard.RIGHT == event.keyCode)
				{
					var lastIndex:int = input.text.length;
					if(lastIndex == input.selectionAnchorPosition && lastIndex == input.selectionActivePosition)
					{
						if(-1 != editPoint)
							editParams[editPoint] = inputIsNull ? null : input.text;
						
						next();
					}
				}
				else if(Keyboard.ENTER == event.keyCode)
				{
					event.preventDefault();
					if(event.shiftKey)//shift + enter 是输入换行符
					{
						input.insertText(File.lineEnding);
					}
					else
					{
						if(-1 != editPoint)
							editParams[editPoint] = inputIsNull ? null : input.text;
						
						apply();
					}
				}
			}
			
			protected function inputOverHandler(e:Event):void
			{
				apply();
				editPoint = -1;
				redraw();
			}
			
			
			/**点击时显示编辑文本框*/
			protected function editHandler(event:MouseEvent):void
			{
				if(method && !method.numParameter)//如果是无参方法，则立即启用，不显示编辑框
				{
					apply();
					return;
				}
				editPoint = 0;
				redraw(TYPE_ALL);
				input.setFocus();
			}
			
			
			protected function dataChangeHandler(event:FlexEvent):void
			{
				if(!data)
					return;
				
				access = data[1] as AccessorProfile;
				method = data[1] as MethodProfile;
				params = data[2];
				editPoint = -1;
				
				redraw();
				
				contextMenu = new ContextMenu;
				var item:ContextMenuItem;
				
				if(data[1])
				{
					item = new ContextMenuItem("删除");
					item.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, deleteHandler);
					contextMenu.addItem(item);
				}
				
			}
			
			protected function deleteHandler(event:ContextMenuEvent):void
			{
				//TODO
			}
			
			
			protected static const TYPE_ALL:String = "all";
			protected static const TYPE_BEGIN:String = "begin";
			protected static const TYPE_END:String = "end";
			
			/**重绘*/
			protected function redraw(type:String = null):void
			{
				if(!data)
					return;
				
				var str:String;
				
				if(-1 == editPoint)//未处于编辑模式
				{
					if(access)
					{
						if(params)
						{
							str = formatStr("{0}:{1} = {2}", access.name, access.type, html(params[0]));
						}
						else
						{
							str = formatStr("{0}:{1}", access.name, access.type);
						}
					}
					else
					{
						str = access.name + "(";
						for(var i:int = 0; i <method.parameters.length; i++)
						{
							if(params && params.length > i)
							{
								str += formatStr("{0}:{1}", html(params[i]), method.parameters[i].type);
							}
							else
							{
								str += html(params[i]);
							}
							
							if(i < method.parameters.length - 1)
							{
								str += ", ";
							}
						}
						str += ")";
					}
					
					setHtml(before_label, str);
					input.parent && removeElement(input);
					after_label.parent && removeElement(after_label);
				}
				else//编辑模式
				{
					if(!editParams)
					{
						editParams = params ? params.concat() : new Vector.<String>;
						editParams.length = access ? 1 : method.numParameter;
					}
					
					//如果字符串包含换行符，则将编辑框设定为多行模式
					str = editParams[editPoint];
					input.multiline = str && (-1 != str.indexOf("\r") || -1 != str.indexOf("\n"));
					
					if(access)
					{
						before_label.text = formatStr("{0}:{1} = ", access.name, access.type);
						input.text = editParams[editPoint];
						after_label.text = "";
					}
					else
					{
						str = access.name + "(";
						for(i = 0; i <method.parameters.length; i++)
						{
							
							if(i == editPoint)
							{
								before_label.text = str;
								str = "";
								setStr(editParams[editPoint]);
							}
							else
							{
								if(params && params.length > i)
								{
									str += formatStr("{0}:{1}", html(params[i]), method.parameters[i].type);
								}
								else
								{
									str += html(params[i]);
								}
							}
							
							if(i < method.parameters.length - 1)
							{
								str += ", ";
							}
						}
						str += ")";
					}
					
					addElement(input);
					addElement(after_label);
				}
				
				
				function html(param:String):String
				{
					if(!param)
						return "<b>null</b>";
					else
						return "\"<b>" + param + "</b>\"";
				}
				
				var textImporter:ITextImporter;
				/**为RichText设置HTML表示的带格式文本*/
				function setHtml(richText:RichText, html:String):void
				{
					textImporter ||= TextConverter.getImporter(TextConverter.TEXT_FIELD_HTML_FORMAT);
					richText.textFlow = textImporter.importToFlow(html);
				}
				
				
				function setStr(str:String):void
				{
					input.text = str || "null";
					inputIsNull = !str;
//					input.setStyle("backgroundColor", str ? 0xFFFFFF : 0xB62222);
					input.setStyle("color", str ? 0x0: 0xff0000);
				}
				
			}
			
			/**应用输入的值*/
			protected function apply():void
			{
				if(!data)
					return;
				
				//检测输入有效性。
				for (var i:int = 0; i < editParams.length; i++) 
				{
					if(undefined === editParams[i])
					{
						if(i + 1 < (access ? 1 : method.numParameterMin))
						{
							logf("[UIDesigner] 参数数量不足：指定了{0}个，但需要{1}个。", i + 1, access ? 1 : method.numParameterMin);
							return;
						}
						editParams.length = i + 1;
						break;
					}
				}
				
				var tp:BasicTargetProfile = data[0];
				if(params)
				{
					var index:int = tp.membersParam.indexOf(params);
					if(-1 == index)//构造方法
					{
						tp.constructorParam = editParams;
					}
					else
					{
						tp.membersParam[index] = editParams;
					}
				}
				else
				{
					tp.membersName.push((data[1] as IMemberProfile).name);
					tp.membersParam.push(editParams);
				}
				
				UIDesignerHost.update();
				
			}
			
			
			/**导航至下一个参数*/
			protected function next():void
			{
				if(data is AccessorProfile)
				{
					editPoint = 0;
				}
				else 
				{
					editPoint ++;
					if(editPoint >= method.numParameter)
					{
						editPoint = 0;
					}
				}
				redraw(TYPE_ALL);
			}
			
			/**导航至上一个参数*/
			protected function previous():void
			{
				if(data is AccessorProfile)
				{
					editPoint = 0;
				}
				else
				{
					editPoint --;
					if(editPoint < 0)
					{
						editPoint = method.numParameter -1;
					}
				}
				redraw(TYPE_ALL);
			}
			
		]]>
	</fx:Script>
	
	<s:RichText id="before_label" width="100%" text="function("/>
	<s:RichEditableText id="input" width="100"/>
	<s:RichText id="after_label" width="50%" text=" )"/>
	
</s:ItemRenderer>
